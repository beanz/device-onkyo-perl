#!/usr/bin/perl
use strict;
use warnings;
use Device::Onkyo;
use Getopt::Long;
use Pod::Usage;


# ABSTRACT: Perl script to control an Onkyo/Integra AV Receiver
# PODNAME: onkyo

=head1 SYNOPSIS

  # send power on
  onkyo

  # increase volume
  onkyo volume up

  # decrease volume with "raw" command
  onkyo MVLDOWN

=head1 DESCRIPTION

This script is an example script to control an Onkyo receiver using the
L<Device::Onkyo> Perl module.

=cut

my $man;
my $help;
my $device = 'discover';
GetOptions('device=s' => \$device,
           'help|?' => \$help, man => \$man) or pod2usage(2);
pod2usage(1) if $help;
pod2usage(-exitstatus => 0, -verbose => 2) if $man;

my $onkyo = Device::Onkyo->new(device => $device);
if (@ARGV) {
  $onkyo->command(join ' ', @ARGV);
  resp($onkyo);
  exit;
}

while (<>) {
  chomp;
  $onkyo->command($_);
  resp($onkyo);
}

sub resp {
  my $onkyo = shift;
  my $cmd = $onkyo->read;
  if ($cmd =~ /^NLS/) {
    $cmd = $onkyo->read;
  }
  print "$cmd\n";
}
